apply plugin: 'org.springframework.boot'

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }

    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    compile {
        // We want to use SLF4J's version of these bindings: jcl-over-slf4j
        // Remove any transitive dependency on Apache's version.
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    runtime {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
}

dependencies {
    // General dependencies
    compile "org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    compile "org.apache.logging.log4j:log4j-web:${log4j_version}"
    compile "org.slf4j:jul-to-slf4j:$slf4j_version"

    // Corda dependencies
    compile "$corda_release_group:corda-jackson:$corda_release_version"
    compile "$corda_release_group:corda-rpc:$corda_release_version"

    testCompile "$corda_release_group:corda-node-driver:$corda_release_version"

    // Local CorDapp dependencies.
    cordapp project(":contracts")
    cordapp project(":workflows")
}

springBoot {
    mainClassName = "net.corda.examples.energyaccount.clients.api.CordaEnergyAccountWebApp"
}

task runBritishEnergyWebCli(type: JavaExec, group: "deployment", dependsOn: assemble) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.examples.energyaccount.clients.api.CordaEnergyAccountWebApp'
    args '--server.port=10050', '--corda.host=localhost:10009', '--corda.user=user1', '--corda.password=test'
}

task runUKPowerWebCli(type: JavaExec, group: "deployment", dependsOn: assemble) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.examples.energyaccount.clients.api.CordaEnergyAccountWebApp'
    args '--server.port=10051', '--corda.host=localhost:10012', '--corda.user=user1', '--corda.password=test'
}

task integrationTest(type: Test, group: "verification", dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}
